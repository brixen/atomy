module(Atomy::Patterns):
  data(Pattern):
    Strict(@body)
    RuntimeClass(@body, @name)
    Predicate(@pattern, @test)

  class(Strict):
    construct(g) := do:
      get(g)
      @body construct(g)
      g send(#new, 1)

    (== b) :=
      b kind-of?(Strict) && @body == b body

    target(g) :=
      @body bytecode(g)

    matches?(g) := do:
      @body bytecode(g)
      g send(#==, 1)

  class(RuntimeClass):
    construct(g) := do:
      get(g)
      @body construct(g)
      @body bytecode(g)
      g send(#name, 0)
      g send(#new, 2)

    (== b) :=
      b kind-of?(RuntimeClass) &&
        @body == b body &&
        @name == b name

    target(g) :=
      @body bytecode(g)

    matches?(g) := do:
      Atomy const-from-string(g, @name)
      g swap
      g kind-of

  class(Predicate):
    construct(g) := do:
      get(g)
      @pattern construct(g)
      @test construct(g)
      g send(#new, 2)

    (== b) :=
      b kind-of?(Predicate) &&
        @pattern == b pattern &&
        @test == b test

    target(g) :=
      if(@pattern)
        then: Any new target(g)
        else: @pattern target(g)

    matches?(g) := do:
      mismatch = g new-label
      done = g new-label

      g dup
      @pattern matches?(g)
      g gif(mismatch)

      `: ~@test ; bytecode(g)
      g send(#block, 0)
      g swap
      g send(#call-on-instance, 1)
      g goto(done)

      mismatch set!
      g pop
      g push-false

      done set!

-- base patterns
module(Atomy::AST):
  Variable to-pattern :=
    Atomy::Patterns::Named new(
      @name
      Atomy::Patterns::Any new
    )

  Primitive to-pattern :=
    Atomy::Patterns::Match new(@value)

  List to-pattern :=
    Atomy::Patterns::List new(
      @elements collect (e): e to-pattern
    )

  Constant to-pattern :=
    Atomy::Patterns::Constant new(self)

  ScopedConstant to-pattern :=
    Atomy::Patterns::Constant new(self)

  ToplevelConstant to-pattern :=
    Atomy::Patterns::Constant new(self)

  Quote to-pattern :=
    Atomy::Patterns::Quote new(@expression)

  Block to-pattern := Atomy::Patterns::Metaclass new(self)

  Particle to-pattern :=
    Atomy::Patterns::Particle new(@name to-sym)

  Send to-pattern :=
    if(@block)
      then:
        Atomy::Patterns::Named new(
          @method-name
          @block contents at(0) to-pattern
        )
      else:
        -- TODO: Unary -> Attribute
        Atomy::Patterns::Unary new(
          @receiver
          @method-name
        )

  QuasiQuote to-pattern :=
    Atomy::Patterns::QuasiQuote new(self)

  String to-pattern :=
    Atomy::Patterns::Match new(@value)

  Node to-pattern :=
    raise("unknown pattern: " + self inspect)
