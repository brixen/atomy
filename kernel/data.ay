macro(&~x):
  BlockPass new(line, x)

macro(*~x):
  Splat new(line, x)

-- now in boot
--macro(@(x: Word)):
  --InstanceVariable new(x line, x name)

macro(@@~(x: Word)):
  ClassVariable new(line, x text)

macro($0):
  GlobalVariable new(line, "0")

macro($~(x: String)):
  GlobalVariable new(line, x value)

macro($~(x: Constant)):
  GlobalVariable new(line, x identifier)

macro($~(x: Word)):
  GlobalVariable new(line, x text)

macro($exception):
  GlobalVariable new(line, "!")

macro($path):
  GlobalVariable new(line, ":")

macro($separator):
  GlobalVariable new(line, "/")

macro(#~(x: Constant)):
  Literal new(line, x identifier to-sym)

macro(#~(x: Word)):
  Literal new(line, x text to-sym)

macro(#~(x: String)):
  Literal new(line, x value to-sym)

macro(#~(x: Primitive)):
  Literal new(line, x value to-s to-sym)

macro(~x for(~e) in(~c)):
  `(~c collect [~e]: ~x)

macro(~x for(~e) in(~c) if(~t)):
  `(~c collect [~e] { ~x } select [~e] { ~t })

Array inject-right(i = _undefined, &block) :=
  reverse inject(i) [x, y]: block [y, x]

.infix right 5 .

macro(~a . ~b): `([~a] + ~b)
