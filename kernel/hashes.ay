namespace(atomy)

macro(#(l: List)):
  keys = []
  values = []

  pairs = l elements dup
  until(pairs empty?):
    pairs pop match:
      `(~k -> ~v) -> do:
        keys << k
        values << v

      (p: Send) -> do:
        while(p is-a?(Send) && p message is-a?(Block)):
          keys << `#~(p receiver name)
          values << p message contents first
          pairs += p message contents [1 .. -1]
          if(p receiver is-a?(Send))
            then: p =! p receiver receiver
            else: break

  Atomy::AST::Hash new(line, keys, values)

export:
  class(Atomy::AST::Hash < Atomy::AST::Node):
    children([#keys], [#values])
    generate

    bytecode(g) := do:
      pos(g)

      g push-cpath-top
      g find-const(#Hash)
      g push-int(@keys size)
      g send(#new-from-literal, 1)

      @keys each-with-index [n, i]:
        g dup
        n compile(g)
        @values [i] compile(g)
        g send("[]=" to-sym, 2)
        g pop
