use("core")
use("define")
use("array")
use("control-flow")

macro(ast { ~*nodes }):
  `(ast(Atomy::AST::Node) { ~*nodes })

macro(ast(~root) { ~*nodes }):
  cs = nodes map [e]:
    e match:
      `(~(name)(~*args)):
        children = Array[]
        attributes = Array[]

        args each [a]:
          a match:
            `[@~name] ->
              attributes << `[#~name]

            `@~name ->
              attributes << `#~name

            _ ->
              children << a

        `(~name = ~root class:
            children(~*children)
            attributes(~*attributes)
            generate)

      _ ->
        `(~e = ~root class:
            generate)

  all =
    if(root != 'Atomy::AST::Node)
      then: `(~root = Class new(Atomy::AST::Node)) . cs
      else: cs

  `(do: ~*all)

Atomy::Format open:
  ast(Segment):
    Chunk([#flags], @text)
    String([#flags])
    Decimal([#flags])
    Hex([#flags])
    Octal([#flags])
    Binary([#flags])
    Radix([#flags])
    Float([#flags])
    Exponent([#flags])
    General([#flags])
    Character([#flags])
    Any([#flags])
    Pluralize(#singular, [#flags], #plural?)
    Lowercase(#content, [#flags])
    Capitalize(#content, [#flags])
    Uppercase(#content, [#flags])
    Justify([#segments], [#flags])
    Skip([#flags])
    Indirection([#flags])
    Iterate(#content, [#flags])
    Break([#flags])
    Conditional([#branches], [#flags], #default?)

  ast(Flag):
    Number(@value?)
    Symbol(@character)
    ZeroPad
    Precision(@value)

  ast:
    Formatter([#segments])

  Segment symbol(m)? :=
    @flags any? [f]:
      f match:
        Symbol -> f character == m
        _ -> false

  Segment precision := do:
    @flags each [f]:
      when(f is-a(Precision)?):
        return(f value)

    nil

  Segment zero-pad? :=
    @flags any? [f]:
      f match:
        ZeroPad -> true
        _ -> false
