module(Atomo::Patterns) {
  data(Pattern) {
    Strict(@body)
    RuntimeClass(@body, @name)
    Predicate(@pattern, @test)
  }

  class(Strict) {
    construct(g) :=
      { get(g)
        @body construct(g)
        g send(#new, 1)
      } call

    == b :=
      b kind-of?(Strict) && @body == b body

    target(g) :=
      @body bytecode(g)

    matches?(g) :=
      { @body bytecode(g)
        g send(#==, 1)
      } call
  }

  class(RuntimeClass) {
    construct(g) :=
      { get(g)
        @body construct(g)
        @body bytecode(g)
        g send(#name, 0)
        g send(#new, 2)
      } call

    == b :=
      b kind-of?(Class) &&
        @body == b body &&
        @name == b name

    target(g) :=
      @body bytecode(g)

    matches?(g) :=
      { Atomo const-from-string(g, @name)
        g swap
        g kind-of
      } call
  }

  class(Predicate) {
    construct(g) :=
      { get(g)
        @pattern construct(g)
        @test construct(g)
        g send(#new, 2)
      } call

    == b :=
      b kind-of?(Predicate) &&
        @pattern == b pattern &&
        @test == b test

    target(g) :=
      if: @pattern
        then: { Any new target(g) }
        else: { @pattern target(g) }

    matches?(g) :=
      { mismatch = g new-label
        done = g new-label

        g dup
        @pattern matches?(g)
        g gif(mismatch)

        `{ ~@test } bytecode(g)
        g swap
        g send(#call-on-instance, 1)
        g goto(done)

        mismatch set!
        g pop
        g push-false

        done set!
      } call
  }
}
