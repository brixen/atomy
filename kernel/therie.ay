namespace(atomy)

dynamic(therie-nesting-level, 0)

symbols(ansi-colored)

module(Therie):
  class(Stats):
    attr-accessor(#passed, #failed)

    initialize := do:
      @passed = 0
      @failed = 0

  { self } indentation :=
    " " * (^therie-nesting-level * 2)

theorize(&as) := do:
  $stats = Therie::Stats new

  as call
  "\n" display

  $stats onto:
    [ "total of", failed + passed, "tests"
      "(" + passed to-s ansi-colored(#green), "passed,"
      if(failed == 0)
        then: "0"
        else: failed to-s ansi-colored(#red)
      "failed)"
    ] join(" ") display

    "\n" display

o should(&check) :=
  unless(o onto(&check)):
    raise("assertion failed for " + o inspect)

x should-be(y) :=
  unless(x == y):
    raise("expected " + y pretty render + ", got " + x pretty render)

x should-raise(y) :=
  x rescue {
    e ->
      unless(e kind-of?(y)):
        raise("expected exception " + y to-s + ", got: " + e to-s)
  } else:
      raise("#should-raise - no exception raised")

describe(x, &body) := do:
  (Therie indentation + "- " + x to-s) print
  let(therie-nesting-level = ^therie-nesting-level + 1):
    body call

it(description, &tests) :=
  { tests call
    (Therie indentation + "✓ " + description)
      ansi-colored(#green) print
  } rescue {
    e -> do:
      $stats failed += 1
      (Therie indentation + "✗ " + description)
        ansi-colored(#red) print
      (Therie indentation + "  ` " + e class name + ": " + e message)
        ansi-colored(#yellow) print
      e backtrace first(5) each [l]:
        (Therie indentation + "    " + l ansi-colored(#cyan)) print
  } else:
      $stats passed += 1

(s: String) ansi-colored(color) := do:
  codes =
    [ #black
      #red
      #green
      #yellow
      #blue
      #magenta
      #cyan
      #white
    ] zip(Range new(0, 7) to-a)

  hash = Hash new
  codes each [[p, n]]:
    hash store(p, n)

  "\e[9" + hash fetch(color) to-s + "m" + s + "\e[0m"
