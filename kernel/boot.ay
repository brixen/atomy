-- a macro so we can actually, you know, send messages
macro(~_ ~Word): to-send

macro(~_ ~(Word)(~*_)): to-send

macro(~(Word)(~*_)): to-send

macro(nil): Primitive new(line, "nil" to-sym)

macro(self): Primitive new(line, "self" to-sym)

macro(true): Primitive new(line, "true" to-sym)

macro(false): Primitive new(line, "false" to-sym)

macro(@~(x: Word)): InstanceVariable new(line, x text)

macro(~x = ~y): Assign new(line, x to-pattern to-node, y)

-- [x, y] { x + y }
macro([~*args] ~Block):
  @right arguments = args
  @right

-- [x, y] &z { x + y }
macro([~*args] &~blk ~Block):
  @right arguments = args
  @right block = blk
  @right

-- &z { x + y }
macro(&~blk ~Block):
  @right block = blk
  @right

-- foo { bar }
macro(~Word ~Block):
  x = @left to-send
  x block = @right
  x

-- foo(a) { bar }
macro(~(Word)(~*args) ~Block):
  x = @left to-send
  x block = @right
  x

-- x foo { bar }
macro(~_ ~Word ~Block):
  x = @left to-send
  x block = @right
  x

-- x foo(a) { bar }
macro(~_ ~(Word)(~*args) ~Block):
  x = @left to-send
  x block = @right
  x

-- collect [x] { x + 1 }
macro(~Word [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- collect(a) [x] { x + 1 }
macro(~(Word)(~*args) [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect [x] { x + 1 }
macro(~_ ~Word [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect(a) [x] { x + 1 }
macro(~_ ~(Word)(~*args) [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- proc-arg
macro(~x &~y):
  @left expand tap [s]:
    s block = BlockPass new(y line, y)

-- particle/symbol block-passing shorthand
macro(~x #~y): `(~x &#~y)

-- foo [bar, ...]
macro(~x [~*ys]):
  to-send tap [x]:
    x message-name = "[]" to-sym
    x arguments = ys + x arguments

-- Foo(...)
macro(~(Constant)(~*args)):
  to-send tap [x]:
    x message-name = @name name

-- Bar::Foo(...)
macro(~(ScopedConstant)(~*args)):
  to-send tap [x]:
    x receiver = @name parent
    x message-name = @name name

macro(_LINE): line
macro(_FILE): File new(line)

macro($path):
  GlobalVariable new(line, ":" to-sym)

$path << File expand-path("../", _FILE)

"operators define cosmetics data comparison meta dynamic control-flow
patterns node data-delta namespace particles maps block errors
format" split each [k]:
  puts("loading " + k)
  require(k)
