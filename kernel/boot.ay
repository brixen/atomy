-- a macro so we can actually, you know, send messages
syntax(_ Variable): to-send

syntax(_ `(~(Variable)(~*_))): to-send

syntax(Variable(*_)): to-send

syntax(nil): Primitive new(line, "nil" to-sym)

syntax(self): Primitive new(line, "self" to-sym)

syntax(true): Primitive new(line, "true" to-sym)

syntax(false): Primitive new(line, "false" to-sym)

syntax(@(x: Variable)): InstanceVariable new(line, x name)

syntax(x = y): Assign new(line, x to-pattern to-node, y)

-- [x, y] { x + y }
syntax(List Block):
  @right arguments = @left elements
  @right

-- foo { bar }
syntax(Variable Block):
  x = `(~(@left)()) to-send
  x block = @right
  x

-- foo(a) { bar }
syntax(`(~(Variable)(~*args)) Block):
  x = @left to-send
  x block = @right
  x

-- x foo { bar }
syntax(`(~_ ~Variable) Block):
  x = @left to-send
  x block = @right
  x

-- x foo(a) { bar }
syntax(`(~_ ~(Variable)(~*args)) Block):
  x = @left to-send
  x block = @right
  x

-- collect [x] { x + 1 }
syntax(`(~Variable [~*ys]) (z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- collect(a) [x] { x + 1 }
syntax(`(~(Variable)(~*args) [~*ys]) (z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect [x] { x + 1 }
syntax(`(~x ~Variable [~*ys]) (z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect(a) [x] { x + 1 }
syntax(`(~_ ~(Variable)(~*args) [~*ys]) (z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- foo [bar, ...]
syntax(x `[~*ys]):
  to-send tap [x]:
    x message-name = "[]"
    x arguments = ys + x arguments

-- Foo(...)
syntax(Constant(*args)):
  to-send tap [x]:
    x message-name = @name identifier

-- Bar::Foo(...)
syntax(ScopedConstant(*args)):
  to-send tap [x]:
    x receiver = @name parent
    x message-name = @name identifier

syntax(_LINE): line
syntax(_FILE): File new(line)

syntax(`(macro(~pattern)) `{ ~*body }):
  `(syntax(~(pattern macro-pattern)): ~*body)

macro(import(~name)): `(Atomy import(~name, "load" to-sym))

"operators define cosmetics data comparison dynamic control-flow patterns
precision node data-delta particles maps block documentation namespaces doc
pretty errors format concurrency io condition therie repl" split each [k]:
  puts("loading " + k)
  import(File expand-path("../", _FILE) + "/" + k)
