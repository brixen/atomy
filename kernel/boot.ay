-- a macro so we can actually, you know, send messages
macro(~_ ~Variable): to-send

macro(~_ ~(Variable)(~*_)): to-send

macro(~(Variable)(~*_)): to-send

macro(nil): Primitive new(line, "nil" to-sym)

macro(self): Primitive new(line, "self" to-sym)

macro(true): Primitive new(line, "true" to-sym)

macro(false): Primitive new(line, "false" to-sym)

macro(@~(x: Variable)): InstanceVariable new(line, x name)

macro(~x = ~y): Assign new(line, x to-pattern to-node, y)

-- [x, y] { x + y }
macro(~List ~Block):
  @right arguments = @left elements
  @right

-- foo { bar }
macro(~Variable ~Block):
  x = @left to-send
  x block = @right
  x

-- foo(a) { bar }
macro(~(Variable)(~*args) ~Block):
  x = @left to-send
  x block = @right
  x

-- x foo { bar }
macro(~_ ~Variable ~Block):
  x = @left to-send
  x block = @right
  x

-- x foo(a) { bar }
macro(~_ ~(Variable)(~*args) ~Block):
  x = @left to-send
  x block = @right
  x

-- collect [x] { x + 1 }
macro(~Variable [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- collect(a) [x] { x + 1 }
macro(~(Variable)(~*args) [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect [x] { x + 1 }
macro(~_ ~Variable [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- [1, 2, 3] collect(a) [x] { x + 1 }
macro(~_ ~(Variable)(~*args) [~*ys] ~(z: Block)):
  z arguments = ys
  x = @left left to-send
  x block = z
  x

-- foo [bar, ...]
macro(~x [~*ys]):
  to-send tap [x]:
    x message-name = "[]"
    x arguments = ys + x arguments

-- Foo(...)
macro(~(Constant)(~*args)):
  to-send tap [x]:
    x message-name = @name identifier

-- Bar::Foo(...)
macro(~(ScopedConstant)(~*args)):
  to-send tap [x]:
    x receiver = @name parent
    x message-name = @name identifier

macro(_LINE): line
macro(_FILE): File new(line)

macro(import(~name)): `(Atomy import(~name, "load" to-sym))

"operators define cosmetics data comparison dynamic control-flow patterns
precision node data-delta particles maps block documentation namespaces doc
pretty errors format concurrency io condition therie repl" split each [k]:
  puts("loading " + k)
  import(File expand-path("../", _FILE) + "/" + k)
