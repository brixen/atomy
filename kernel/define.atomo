macro (x = y)
  Atomo::AST::Assign new(x line, x, y)

macro (x =! y)
  Atomo::AST::Set new(x line, x, y)

macro (x := y)
  Atomo::AST::Define new(x line, x, y)

macro (define(x, &y))
  Atomo::AST::Define new(x line, x, y body)

macro (do(&b)) `(~b call)

macro (x **= y) `(~x =! (~x ** ~y))
macro (x *= y) `(~x =! (~x * ~y))
macro (x <<= y) `(~x =! (~x << ~y))
macro (x >>= y) `(~x =! (~x >> ~y))
macro (x &&= y) `(~x =! (~x && ~y))
macro (x &= y) `(~x =! (~x & ~y))
macro (x ||= y) `(~x =! (~x || ~y))
macro (x |= y) `(~x =! (~x | ~y))
macro (x += y) `(~x =! (~x + ~y))
macro (x -= y) `(~x =! (~x - ~y))
macro (x /= y) `(~x =! (~x / ~y))
macro (x ^= y) `(~x =! (~x ^ ~y))
macro (x %= y) `(~x =! (~x % ~y))

macro (x match(&b)) do :
  n = Atomo::AST::Match new(x line, x, b)
  `(: ~n; call)

macro (class(`(<< ~obj), &body))
  Atomo::AST::Metaclass new(obj line, obj, body body)

macro (class(`(~name < ~super), &body))
  Atomo::AST::Class new(name line, name, body body, super)

macro (class(name, &body))
  Atomo::AST::Class new(name line, name, body body)

macro (module(name, &body))
  Atomo::AST::Module new(name line, name, body body)

macro (data(root, &children)) do:
  cs = children contents map (e):
    e match:
      Atomo::AST::Send -> do:
        init =
          Atomo::AST::Send new(
            0
            Atomo::AST::Primitive new(0, #self)
            e arguments
            "initialize"
          )

        accessors =
          Atomo::AST::Send new(
            0
            Atomo::AST::Primitive new(0, #self)
            e arguments map (a):
              name = a receiver name to-s
              Atomo::AST::Particle new(0, name)
            "attr_accessor"
          )

        name =
          Atomo::AST::Constant new(0, e method-name)

        `(class(~name < ~root):
            ~accessors
            ~init := #ok)

      Atomo::AST::Constant ->
        `(class(~e < ~root):)

      Atomo::AST::ScopedConstant ->
        `(class(~e < ~root):)

      Atomo::AST::ToplevelConstant ->
        `(class(~e < ~root):)

      _ -> raise("unknown module name: " + e to-s)

  decls = Atomo::AST::Block new(
    root line
    [`(class(~root):)] + cs
    []
  )

  `(~decls call)
