namespace(atomy/pretty)

comma-delim(ds) :=
  hsep(comma punctuate(ds))

ident(n) := text(n tr("_", "-") gsub("_ns_", "/"))

module(Atomy::AST):
  Block unamb := pretty
  ClassVariable unamb := pretty
  Constant unamb := pretty
  GlobalVariable unamb := pretty
  InstanceVariable unamb := pretty
  List unamb := pretty
  Particle unamb := pretty
  Primitive unamb := pretty
  QuasiQuote unamb := pretty
  Quote unamb := pretty
  ScopedConstant unamb := pretty
  Splice unamb := pretty
  String unamb := pretty
  ToplevelConstant unamb := pretty
  Unary unamb := pretty
  Unquote unamb := pretty
  Variable unamb := pretty
  Node unamb := Doc parens(pretty)

export-to(atomy):
  Object pretty := Doc text(inspect)

  Object show := pretty render

  Array pretty :=
    doc: brackets(comma-delim(self all-pretty))

  Particle pretty := doc:
    wildcard = [c]:
      if(c equal?(_undefined))
        then: '_
        else: c

    operator? = self message to-s !~ r"^[a-z_]"

    args = self arguments collect(&#(wildcard [_]))

    msg+args =
      if(operator?)
        then:
          text(self message to-s) <+> args first unamb
        else:
          ident(self message to-s) <>
            parens(comma-delim(args all-pretty))

    partial =
      if(self receiver equal?(_undefined))
        then:
          if(operator?)
            then: parens(msg+args)
            else: msg+args
        else: parens(self receiver unamb <+> msg+args)

    text("#") <> partial

  Particle to-s := pretty render

  Symbol pretty := do:
    s = to-s
    if(s =~ r(u)"^[\p{Ll}_]([\p{L}\d\p{S}!@#%&*\-\\:.\/\?])*$")
      then: Doc text("#" + s)
      else: Doc text("#") <> s pretty

  Hash pretty := doc:
    text("#") <>
      brackets(
        comma-delim(
          self to-a collect [[k, v]]:
            k unamb <+> text("->") <+> v unamb))

  module(Atomy::AST):
    Assign pretty := doc: @lhs unamb <+> text("=") <+> @rhs unamb

    BinarySend pretty := doc:
      @lhs unamb <+> text(@operator) <+> @rhs unamb

    Block pretty := doc:
      args =
        if(@arguments empty?)
          then: empty
          else: parens(comma-delim(@arguments all-pretty))

      args <> lbrace <+> comma-delim(@contents all-pretty) <+> rbrace

    BlockPass pretty := doc: text("&") <> @body unamb

    Class pretty := doc:
      sub =
        if(@superclass kind-of?(Primitive) && @superclass value == #nil)
          then: empty
          else: text("<") <+> @superclass unamb

      name =
        @name match:
          Rubinius::AST::ClassName -> text(@name) name to-s
          _ -> @name unamb

      body = lbrace <+> semi-delim(@body) expressions <+> rbrace

      text("class") <> text("(") <> name <+> sub <> text(")") <+> body

    ClassVariable pretty := doc: ident(name to-s)

    Constant pretty := doc: text(@identifier)

    Define pretty := doc: @lhs unamb <+> text(":=") <+> @body unamb

    GlobalVariable pretty := doc: ident(name to-s)

    InstanceVariable pretty := doc: ident(name to-s)

    List pretty := doc: brackets(comma-delim(@elements all-pretty))

    Macro pretty := doc:
      text("macro") <+> parens(@pattern pretty) <+> @body pretty

    Particle pretty := doc: text("#") <> ident(@name)

    Primitive pretty := doc:
      @value match:
        -- TODO: glitch with #doc/#onto
        #"self" -> text("self")
        #true -> text("true")
        #false -> text("false")
        #nil -> text("nil")
        x -> x pretty

    QuasiQuote pretty := doc: text("`") <> @expression unamb

    Quote pretty := doc: text("'") <> @expression unamb

    ScopedConstant pretty := doc:
      @parent unamb <> text("::") <> text(name to-s)

    Send pretty := doc:
      block = if(@block) then: @block pretty; else: empty

      args =
        if(@arguments empty?)
          then: empty
          else: parens(comma-delim(@arguments all-pretty))

      if(@receiver kind-of?(Primitive) && @receiver value == #"self")
        then: ident(@method-name) <> args <+> block
        else:
          @receiver unamb <+> ident(@method-name) <> args <+> block

    Splice pretty := doc: text("~*") <> @expression unamb

    String pretty := @value pretty

    ToplevelConstant pretty := doc: text("::") <> text(name to-s)

    Unary pretty := doc: text(@operator) <> @receiver unamb

    Unquote pretty := doc: text("~") <> @expression unamb

    Variable pretty := doc: ident(@name)

    InlinedBody pretty := doc:
      parens(hsep(comma punctuate(@expressions collect [e]: e pretty)))

    Or pretty := `(~@a || ~@b) pretty

    And pretty := `(~@a && ~@b) pretty

    Ensure pretty := doc:
      @body pretty <+> text("ensuring:") <+> @ensure pretty


Array all-pretty := collect [x]: x pretty
